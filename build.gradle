buildscript {
    ext {
        kotlinVersion = '1.3.11'
        springBootVersion = '2.0.5.RELEASE'
        dockerVersion = '0.20.1'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:${dockerVersion}")
    }
}

allprojects {
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    
    group = "org.burbridge.${rootProject.name}.${project.name}"
    version = '0.0.3-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        springBootVersion = '2.0.5.RELEASE'
        springCloudVersion = 'Finchley.RELEASE'
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    repositories {
        mavenCentral()
        maven {
            url "https://repo.spring.io/milestone"
        }
    }

    dependencies {
        compile('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
        compile('org.jetbrains.kotlin:kotlin-reflect')
        compile('io.github.microutils:kotlin-logging:1.6.22')
        compile('com.fasterxml.jackson.module:jackson-module-kotlin')

        testCompile('org.springframework.boot:spring-boot-starter-test') {
            exclude module: 'junit'
        }
        testImplementation('org.junit.jupiter:junit-jupiter-api')
        testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }

    test {
        useJUnitPlatform()
    }

    jacoco {
        toolVersion = '0.8.2'
    }

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/kotlin/main")
        }
    }
}
